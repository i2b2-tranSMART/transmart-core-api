// run ./gradlew clean build artifactoryPublish

buildscript {
	repositories {
		mavenLocal()
		jcenter()
	}
	dependencies {
		classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.+'
	}
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

description = 'tranSMART Core API'
group = 'org.transmartproject'
sourceCompatibility = 1.7
targetCompatibility = 1.7

version file('version.txt').text.trim()

repositories {
	mavenLocal()
	jcenter()
}

dependencies {
	compile 'com.google.guava:guava:14.0.1'
	compile 'org.codehaus.groovy:groovy-all:2.1.9' // the version used in Grails 2.3.11
	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0' // the version used in Grails 2.3.11
}

ext.isSnapshot = version.contains('SNAPSHOT')

task sourceJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives sourceJar
	archives javadocJar
}

artifactory {
	contextUrl = System.getenv('ARTIFACTORY_I2B2TRANSMART_CONTEXTURL') ?:
	             'http://ec2-35-170-59-132.compute-1.amazonaws.com:8080/artifactory/'
	publish {
		repository {
			repoKey = isSnapshot ? 'libs-snapshots' : 'libs-releases'
			username = System.getenv('ARTIFACTORY_I2B2TRANSMART_USERNAME')
			password = System.getenv('ARTIFACTORY_I2B2TRANSMART_PASSWORD')
			maven = true
		}
		defaults {
			publications 'mavenJava'
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourceJar {
				classifier 'sources'
			}

			artifact javadocJar {
				classifier 'javadoc'
			}
		}
	}
}
